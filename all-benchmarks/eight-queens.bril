# ARGS: 8
@main(input: int) {
  n: int = id input;
  zero: int = const 0;
  icount: int = id zero;
  site: ptr<int> = alloc n;
  result: int = call @queen zero n icount site;
  print result;
  free site;
}
@queen(n: int, queens: int, icount: int, site: ptr<int>): int {
  one: int = const 1;
  ite: int = id one;
  ret_cond: bool = eq n queens;
  br ret_cond .next.ret .for.cond;
.next.ret:
  icount: int = add icount one;
  ret icount;
.for.cond:
  for_cond_a: bool = le ite queens;
  br for_cond_a .for.body .next.ret.1;
.for.body:
  nptr: ptr<int> = ptradd site n;
  store nptr ite;
  is_valid: bool = call @valid n site;
  br is_valid .rec.func .next.loop;
.rec.func:
  n_b: int = add n one;
  icount: int = call @queen n_b queens icount site;
.next.loop:
  ite: int = add ite one;
  jmp .for.cond;
.next.ret.1:
  ret icount;
}
@valid(n: int, site: ptr<int>): bool {
  zero: int = const 0;
  one: int = const 1;
  true: bool = eq one one;
  false: bool = eq zero one; 
  ite: int = id zero;
.for.cond:
  for_cond: bool = lt ite n;
  br for_cond .for.body .ret.end;
.for.body:
  iptr: ptr<int> = ptradd site ite;
  nptr: ptr<int> = ptradd site n;
  help_a: int = const 500;
  vali: int = load iptr;
  valn: int = load nptr;
  eq_cond_a: bool = eq vali valn;
  br eq_cond_a .true.ret.0 .false.else;
.true.ret.0:
  ret false;
.false.else:
  sub_a: int = sub vali valn;
  sub_b: int = sub valn vali;
  sub_c: int = sub n ite;
  eq_cond_b: bool = eq sub_a sub_c;
  eq_cond_c: bool = eq sub_b sub_c;
  eq_cond_bc: bool = or eq_cond_b eq_cond_c;
  br eq_cond_bc .true.ret.1 .false.loop;
.true.ret.1:
  ret false;
.false.loop:
  ite: int = add ite one;
  jmp .for.cond;
.ret.end:
  ret true;
}